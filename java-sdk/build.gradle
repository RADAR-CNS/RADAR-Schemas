//---------------------------------------------------------------------------//
// Configuration                                                             //
//---------------------------------------------------------------------------//

plugins {
    id 'com.github.davidmc24.gradle.plugin.avro-base' version '1.1.0'
    id("io.github.gradle-nexus.publish-plugin") version "1.1.0"
}

allprojects {
    version = '0.7.1-SNAPSHOT'
    group = 'org.radarbase'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    // Configuration
    ext.githubRepoName = 'RADAR-base/RADAR-Schemas'
    ext.githubUrl = "https://github.com/${githubRepoName}.git"
    ext.issueUrl = "https://github.com/${githubRepoName}/issues"
    ext.website = 'https://radar-base.org'

    ext {
        avroVersion = '1.9.2'
        argparseVersion = '0.8.1'
        jacksonVersion = '2.12.3'
        jacksonModuleVersion = '2.12.3'
        radarJerseyVersion = "0.6.2"

        junitVersion = '4.13.2'
        confluentVersion = '6.1.1'
        kafkaVersion = '6.1.1-ce'
        okHttpVersion = '4.9.1'
        radarCommonsVersion = '0.13.2'
        slf4jVersion = '1.7.30'
        javaxValidationVersion = '2.0.1.Final'
        logbackVersion = '1.2.3'
    }

    repositories {
        mavenCentral()
        maven { url 'https://packages.confluent.io/maven/' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    }

    // Generated avro files
    ext.avroOutputDir = file('src/generated/java')

    idea {
        module {
            downloadSources = true
        }
    }

    task downloadDependencies {
        description "Pre-downloads *most* dependencies"
        doLast {
            configurations.getAsMap().each { name, config ->
                println "Retrieving dependencies for $name"
                try {
                    config.files
                } catch (e) {
                    project.logger.info e.message // some cannot be resolved, silentlyish skip them
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username = project.hasProperty("ossrh.user") ? project.property("ossrh.user") : System.getenv("OSSRH_USER")
            password = project.hasProperty("ossrh.password") ? project.property("ossrh.password") : System.getenv("OSSRH_PASSWORD")
        }
    }
}

wrapper {
    gradleVersion '7.0'
}
