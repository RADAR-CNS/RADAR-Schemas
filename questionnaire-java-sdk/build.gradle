ext.artifactName = 'radar-schemas-questionnaire'
ext.description = 'RADAR Schemas Questionnaire SDK'

//---------------------------------------------------------------------------//
// AVRO file manipulation                                                    //
//---------------------------------------------------------------------------//
task generateAvro(type: com.commercehub.gradle.plugin.avro.GenerateAvroJavaTask) {
    source(rootProject.file('questionnaire'))
    outputDir avroOutputDir
}

compileJava.dependsOn generateAvro

//---------------------------------------------------------------------------//
// Publishing                                                                //
//---------------------------------------------------------------------------//

publishing {
    publications {
        RadarQuestionnairePublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId artifactName
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', description)
                root.appendNode('name', artifactName)
                root.appendNode('url', vcsUrl)
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    override = false
    publications = ['RadarQuestionnairePublication']
    pkg {
        repo = project.group
        name = rootProject.name
        userOrg = artifactName
        licenses = ['Apache-2.0']
        websiteUrl = website
        issueTrackerUrl = issueUrl
        vcsUrl = vcsUrl
        githubRepo = githubRepo
        githubReleaseNotesFile = 'README.md'
        version {
            name = project.version
            desc = description
            vcsTag = System.getenv('TRAVIS_TAG')
            released = new Date()
        }
    }
}
